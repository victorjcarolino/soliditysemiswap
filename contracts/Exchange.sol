// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.7;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract Exchange {
    ERC20 public erc20Token; // The ERC-20 token assigned to this instance of Exchange
    uint public totalLiquidityPositions;
    uint public K; // Constant product
    mapping(address => uint) public liquidityPositions; // hash map of addresses to their liquidity positions

    constructor(address _erc20Address) {
        erc20Token = ERC20(_erc20Address);
    }

    /// GRADERNOTE: This function minimumWeiAndERC20ToProvide() is not in the spec, but we noticed that when providing too little an amount 
    /// of ERC-20 tokens and wei, the provided liquidity points to the trader would return 0. For the purposes
    /// of our testing, we implemented this function to provide estimates for the amount of both ERC-20 tokens 
    /// and ether to provide in order to recieve 1 liqiudity point. 
    // Calculates the amount of Ether and ERC20 to provide in order to gain 1 liquidity point 
    function minimumWeiAndERC20ToProvide() public view returns (uint amountWei, uint amountERC20) {
        // Each are padded by 1 and multiplied by 2 just to ensure that no truncation errors occur when passing in the values
        amountERC20 = 2 * erc20Token.balanceOf(address(this)) / totalLiquidityPositions + 1;
        amountWei = 2 * address(this).balance / totalLiquidityPositions + 1;
    }

    // spec-required functions
    function provideLiquidity(uint _amountERC20Token) public payable returns (uint liquidity) {
        require(msg.value > 0, "Error: Must input greater than 0 Wei");
        require(_amountERC20Token > 0, "Error: Must input greater than 0 ERC20-Tokens");

        uint weiBalanceBefore = address(this).balance - msg.value;
        uint erc20BalanceBefore = erc20Token.balanceOf(address(this));

        // Transfer ERC20 tokens from the user to the contract
        bool sent = erc20Token.transferFrom(msg.sender, address(this), _amountERC20Token);
        require(sent, "Error: ERC20 transfer failed");

        if (totalLiquidityPositions == 0) {
            liquidity = 100; // if first time it will start with 100 liquidity
        } else {
            uint weiReserve = weiBalanceBefore;
            uint tokenReserve = erc20BalanceBefore;
                
            /// GRADERNOTE: Below is a solution for maintaining roughly equal ratios due to a truncation error when 
            /// estimating eth (wei) and erc-20 tokens needed to provide to maintain proper ratio. The error is expressed
            /// as a difference of 1 wei when providing liquidity, swap erc-20 for eth, and provide liquidity again.
            /// Our fix allows for a small value epsilon of rounding error that must be below 0.1% of the greater ratio.
            /// We spoke with Prof. Korth on how to solve this issue, and we concluded that while there may be an opportunity
            /// for extracting value from this mechanism, the gas required to complete this attack would 
            /// outweigh the potential gains for our purposes
            uint epsilon;
            uint ceiling; // The ceiling is 0.1% of the greater product
            // Ensure that the ratio of ETH to ERC20 is maintained with allowance for small epsilon deviation
            if ((_amountERC20Token * weiReserve) < (msg.value * tokenReserve)) {
                ceiling = (msg.value * tokenReserve) / 1000;
                epsilon = (msg.value * tokenReserve) - (_amountERC20Token * weiReserve);
            }
            else if ((_amountERC20Token * weiReserve) > (msg.value * tokenReserve)) {
                ceiling = (_amountERC20Token * weiReserve) / 1000;
                epsilon = (_amountERC20Token * weiReserve) - (msg.value * tokenReserve);
            }
            else {
                ceiling = 1;
                epsilon = 0;
            }
            require(epsilon <= ceiling, "Error: Must maintain Wei/ERC20 ratio");

            // Calculate liquidity based on the proportional amount of ETH deposited
            liquidity = totalLiquidityPositions * _amountERC20Token / erc20BalanceBefore;
        }
        require(liquidity > 0, "Error: Deposit would mint zero liquidity positions");
        liquidityPositions[msg.sender] += liquidity;
        totalLiquidityPositions += liquidity;

        // Update K after liquidity is added, based on the new balances
        K = (address(this).balance) * (erc20Token.balanceOf(address(this)));

        emit LiquidityProvided(msg.sender, _amountERC20Token, msg.value, liquidity); 
        return liquidity;
    }

    function estimateEthToProvide(uint _amountERC20Token) public view returns (uint amountEth) {
        require(_amountERC20Token > 0, "Error: ERC20 token must be greater than 0.");
        uint contractEthBalance = address(this).balance;
        uint contractERC20TokenBalance = erc20Token.balanceOf(address(this));
        // Check if there's any ERC20 token balance in the contract to avoid division by zero
        if (contractERC20TokenBalance == 0) {
            return 0;
        }
        // Calculate an estimate of the amount of Wei required to maintain the current ratio
        amountEth = contractEthBalance * _amountERC20Token / contractERC20TokenBalance;
        return amountEth;
    }

    function estimateERC20TokenToProvide(uint _amountEth) public view returns (uint amountERC20Token) {
        require(_amountEth > 0, "Error: Wei must be greater than 0.");
        uint contractEthBalance = address(this).balance;
        uint contractERC20TokenBalance = erc20Token.balanceOf(address(this));
        if (contractEthBalance == 0) {
            return 0;
        }
        // Calculate the amount of ERC20 tokens required to maintain the current ratio
        amountERC20Token = contractERC20TokenBalance * _amountEth / contractEthBalance;
        return amountERC20Token;
    }


    function getMyLiquidityPositions() external view returns (uint) {
        return liquidityPositions[msg.sender];
    }

    function withdrawLiquidity(uint _liquidityPositionsToBurn) public {
        require(_liquidityPositionsToBurn > 0, "Error: Cannot burn zero liquidity positions");
        require(liquidityPositions[msg.sender] >= _liquidityPositionsToBurn, "Error: Not enough liquidity positions to burn");

        uint contractEthBalance = address(this).balance;
        uint contractERC20TokenBalance = erc20Token.balanceOf(address(this));

        uint amountEthToSend = _liquidityPositionsToBurn * contractEthBalance / totalLiquidityPositions;
        uint amountERC20ToSend = _liquidityPositionsToBurn * contractERC20TokenBalance / totalLiquidityPositions;

        liquidityPositions[msg.sender] -= _liquidityPositionsToBurn;
        totalLiquidityPositions -= _liquidityPositionsToBurn;

        // Using transfer for ETH to send to the user
        payable(msg.sender).transfer(amountEthToSend);

        // ERC20 transfer to the user
        require(erc20Token.transfer(msg.sender, amountERC20ToSend), "Error: Failed to send ERC20 tokens");

        // Update the K value after liquidity is removed
        K = (contractEthBalance - amountEthToSend) * (contractERC20TokenBalance - amountERC20ToSend);

        // Emitting the event with the amount of ERC20 tokens and Ether sent, and liquidity positions burned
        emit LiquidityWithdrew(amountERC20ToSend, amountEthToSend, _liquidityPositionsToBurn);
    }

    function swapForEth(uint _amountERC20Token, uint minEthOut, uint deadline) public returns (uint ethToSend) {
        require(block.timestamp <= deadline, "Error: Swap expired");
        uint contractEthBalance = address(this).balance;
        uint contractERC20TokenBalance = erc20Token.balanceOf(address(this));

        require(_amountERC20Token > 0, "Error: Must input more than 0 ERC20 Token.");
        require(contractERC20TokenBalance > 0, "Error: Insufficient liquidity.");
        
        // Transfer ERC20 tokens from the user to the contract
        require(erc20Token.transferFrom(msg.sender, address(this), _amountERC20Token), "Error: ERC20 transfer failed");

        // Calculate contractEthBalanceAfterSwap using the constant product formula
        uint contractERC20TokenBalanceAfterSwap = contractERC20TokenBalance + _amountERC20Token;
        uint contractEthBalanceAfterSwap = K / contractERC20TokenBalanceAfterSwap;
        ethToSend = contractEthBalance - contractEthBalanceAfterSwap;

        // Checks to prevent swaps that would result in no ETH sent to the user
        require(ethToSend > 0 && ethToSend <= contractEthBalance, "Error: Invalid swap request");
        require(ethToSend >= minEthOut, "Error: Minimum output not met");

        // Send ETH to the user
        payable(msg.sender).transfer(ethToSend);

        emit SwapForEth(_amountERC20Token, ethToSend);

        return ethToSend;
    }

    function estimateSwapForEth(uint _amountERC20Token) public view returns (uint ethToSend) {
        require(_amountERC20Token > 0, "Error: Must input more than 0 ERC20 Token.");
        
        uint contractEthBalance = address(this).balance;
        uint contractERC20TokenBalance = erc20Token.balanceOf(address(this));
        require(contractERC20TokenBalance > 0, "Error: Insufficient liquidity.");

        // Calculate contractEthBalanceAfterSwap using the constant product formula
        uint contractERC20TokenBalanceAfterSwap = contractERC20TokenBalance + _amountERC20Token;
        uint contractEthBalanceAfterSwap = K / contractERC20TokenBalanceAfterSwap;
        ethToSend = contractEthBalance - contractEthBalanceAfterSwap;

        // Checks to ensure the estimation doesn't suggest a swap that would result in no ETH sent
        require(ethToSend > 0 && ethToSend <= contractEthBalance, "Error: Invalid swap estimate");

        return ethToSend;
    }

    function swapForERC20Token(uint minTokenOut, uint deadline) public payable returns (uint ERC20TokenToSend) {
        require(block.timestamp <= deadline, "Error: Swap expired");
        require(msg.value > 0, "Error: Must deposit more than 0 Wei.");
        uint contractERC20TokenBalance = erc20Token.balanceOf(address(this));
        uint contractERC20TokenBalanceAfterSwap = K / (address(this).balance);
        ERC20TokenToSend = contractERC20TokenBalance - contractERC20TokenBalanceAfterSwap;
        require(ERC20TokenToSend > 0 && ERC20TokenToSend >= minTokenOut, "Error: Minimum output not met");

        // Transfer ERC20 tokens from the contract to the caller
        require(erc20Token.transfer(msg.sender, ERC20TokenToSend), "Error: Failed to send ERC20 tokens");

        emit SwapForERC20Token(ERC20TokenToSend, msg.value);

        return ERC20TokenToSend;
    }

    function estimateSwapForERC20Token(uint _amountEth) public view returns (uint ERC20TokenToSend) {
        require(_amountEth > 0, "Error: ETH amount must be greater than 0.");
        uint contractEthBalance = address(this).balance;
        uint contractERC20TokenBalance = erc20Token.balanceOf(address(this));
        uint contractERC20TokenBalanceAfterSwap = K / (contractEthBalance + _amountEth);
        ERC20TokenToSend = contractERC20TokenBalance - contractERC20TokenBalanceAfterSwap;

        return ERC20TokenToSend;
    }

    // Events for logging exchange actions
    event LiquidityProvided(address provider, uint amountERC20TokenDeposited, uint amountEthDeposited, uint liquidityPositionsIssued);
    event LiquidityWithdrew(uint amountERC20TokenWithdrew, uint amountEthWithdrew, uint liquidityPositionsBurned);
    event SwapForEth(uint amountERC20TokenDeposited, uint amountEthWithdrew);
    event SwapForERC20Token(uint amountERC20TokenWithdrew, uint amountEthDeposited);
}
